Package compiler;


Helpers

	sp = ' ';												//space
  	lf = 0x000a; 											//line feed
  	cr = 0x000d;											//carriage return
  	ff = 0x000c;											//form feed
  	ht = 0x0009; 											//horizontal tab
	
	all = [0 .. 0xffff];
	letter = [['a'..'z'] + ['A'..'Z']];
	digit = ['0'..'9'];
	hex_digit = (digit | 'a' | 'b' | 'c' | 'd' | 'e' | 'f');
	hex_num = 'x' hex_digit hex_digit;
	


	escape = '\' ('n' | 't' | 'r' | '0' | '\' | ''' | '"' | hex_num);
	
	lterminator = (lf | cr | cr lf);
	
	comment = ([all - [lf + [cr + '$']]] [all - [lf + cr]]* lterminator?);
	line_comment = '$' ( comment | lterminator?);
	
	white_space = (sp | ht | ff | lterminator)*;
	    
	a = [all - '$'];
	middle = ('$'? a)* ;									// a* ('$' a a*)*
	multiline_comment =  '$' '$' middle '$' '$' ;


Tokens
	
	and = 'and';									//keys
	char = 'char';
	divide = 'div';
	do = 'do';
	else = 'else';
	fun = 'fun';
	if = 'if';
	integer = 'int';
	mod = 'mod';
	not = 'not';
	nothing = 'nothing';
	or = 'or';
	ref = 'ref';
	return = 'return';
	then = 'then';
	var = 'var';
	while = 'while';	
	
	id = letter (letter | digit | '_')*;			//Identifiers
	
	number = digit+;        						//Integer Numbers
	
	conchar = ''' (all | escape) ''';				//Constant Characters
	
	string = '"' ([all - '"'] | '\"')* '"';
  
	plus = '+';										//Symbolic operators
	minus = '-';
	mult = '*';
	div = '/';
	nequal = '#';
	equal = '=';
	less = '<';
	greater = '>';
	lesseq = '<=';
	greatereq = '>=';
	
	left_par = '(';									//Separators 
	right_par = ')';
	left_bracket = '[';
	right_bracket = ']';
	left_brace = '{';
	right_brace = '}';
	comma = ',';
	semicolon = ';';
	colon = ':';
	arrow = '<-';

	multiline_comment = multiline_comment;
	line_comment = line_comment;
	white_space = white_space;




Ignored Tokens
	
	white_space,
	line_comment,
	multiline_comment;	
	
  
Productions


	program {-> program} = func_def												{-> New program(func_def.func_def)} ;

	func_def {-> func_def} = header local_def* block 							{-> New func_def(header.header, [local_def.local_def], block.block)} ;


	header {-> header} = 
			{withpars} fun id left_par fpar_def_list right_par colon ret_type	{-> New header.withpars(id, [fpar_def_list.fpar_def], ret_type.ret_type)}
		|	{withoutpars} fun id left_par right_par colon ret_type				{-> New header.withoutpars(id, ret_type.ret_type)}
		;

	fpar_def_list {-> fpar_def*} = fpar_def fpar_def_list_tail*					{-> [fpar_def.fpar_def, fpar_def_list_tail.fpar_def]} ;
	fpar_def_list_tail {-> fpar_def} = semicolon fpar_def						{-> fpar_def.fpar_def} ;

	
 	fpar_def {-> fpar_def} = 
			{withref} ref idlist colon fpar_type								{-> New fpar_def.withref(ref, [idlist.id], fpar_type.fpar_type)} 
		|	{withoutref} idlist colon fpar_type									{-> New fpar_def.withoutref([idlist.id], fpar_type.fpar_type)}
		;
	
	idlist {-> id*} = id idlist_tail*											{-> [id, idlist_tail.id]} ;
	idlist_tail {-> id} = comma id												{-> id} ;


	data_type {-> data_type} = 
			{int} integer 														{-> New data_type.int(integer)}
		|	{char} char															{-> New data_type.char(char)}
		;

	ret_type {-> ret_type} = 
			{data} data_type													{-> New ret_type.data(data_type.data_type)}
		|	{none} nothing														{-> New ret_type.none(nothing)}
		; 
	
	
	fpar_type {-> fpar_type} =
			
			{noarray} data_type 												{-> New fpar_type.noarray(data_type.data_type)}					
		|	{array_first} data_type helper										{-> New fpar_type.array_first(data_type.data_type, helper.helper)}
		|	{array_firstsec} data_type helper numlist							{-> New fpar_type.array_firstsec(data_type.data_type, helper.helper, [numlist.number])}
		|	{array_sec} data_type numlist										{-> New fpar_type.array_sec(data_type.data_type, [numlist.number])}
		;
	
	helper {-> helper} = left_bracket right_bracket								{-> New helper(left_bracket, right_bracket)} ;
	
	numlist {-> number*} = left_bracket number right_bracket numlist_tail*		{-> [number, numlist_tail.number]};
	numlist_tail {-> number} = left_bracket number right_bracket 				{-> number} ;




	local_def {-> local_def} =
	
			{funcdef} func_def									{-> New local_def.funcdef(func_def.func_def)}
		|	{funcdecl} func_decl								{-> New local_def.funcdecl(func_decl.func_decl)}
		|	{vardef} var_def									{-> New local_def.vardef(var_def.var_def)}
		;


	type {-> type} =
			{array} data_type numlist							{-> New type.array(data_type.data_type, [numlist.number])} 
		|	{noarray} data_type									{-> New type.noarray(data_type.data_type)} 
		;


	var_def {-> var_def} = var idlist colon type semicolon		{-> New var_def([idlist.id], type.type)} ;

	func_decl {-> func_decl} = header semicolon 				{-> New func_decl(header.header)} ;

	block {-> block} = left_brace stmt* right_brace 			{-> New block([stmt.stmt])} ;




/////////////////// STMTS ///////////////////////

	stmt {-> stmt} =
		
			{part} partstmt										{-> partstmt.stmt}
		|	{if} ifstmt											{-> ifstmt.stmt}
		|	{while} whilestmt									{-> whilestmt.stmt}
		;

	partstmt {-> stmt} = 
	
			{none} semicolon									{-> New stmt.none(semicolon)}
		|	{assignment} l_value arrow expr semicolon			{-> New stmt.assignment(l_value.expr, expr.expr)}
		|	{block} block 										{-> New stmt.block(block.block)}
		|	{funcall} func_call semicolon						{-> New stmt.func_call(func_call.expr)}
		|	{returnepxr} return expr semicolon					{-> New stmt.returnexpr(expr.expr)}
		|	{return} return semicolon							{-> New stmt.return(semicolon)}
		;
	
	ifstmt {-> stmt} = 
			
			{noelse} if cond then stmt							{-> New stmt.if(cond.cond, stmt.stmt)}
		|	{else} if cond then withelse else stmt				{-> New stmt.ifelse(cond.cond, withelse.stmt, stmt.stmt)}
		; 

	withelse {-> stmt} =
			
			if cond then [left]:withelse else [right]:withelse	{-> New stmt.ifelse(cond.cond, left.stmt, right.stmt)}
		|	{while} while cond do withelse						{-> New stmt.while(cond.cond, withelse.stmt)}
		|	{partstmt} partstmt									{-> partstmt.stmt}
		;
			
	whilestmt {-> stmt} = while cond do stmt					{-> New stmt.while(cond.cond, stmt.stmt)} ;

/////////////////////////////////////////////////////////////////////


	
////////// EXRPESSIONS ///////////

	expr {-> expr} = 
			
			{plus} expr plus term 				{-> New expr.plus(expr.expr, term.expr)}
		|	{minus} expr minus term 			{-> New expr.minus(expr.expr, term.expr)}
		|	{term} term 						{-> term.expr} 
		;


	term {-> expr} =
	
			{mult} term mult factor				{-> New expr.mult(term.expr, factor.expr)}
		|	{div} term divide factor			{-> New expr.div(term.expr, factor.expr)}
		|	{mod} term mod factor				{-> New expr.mod(term.expr, factor.expr)}
		|	{factor} factor						{-> factor.expr}
		;

	
	factor {-> expr} =
			
			{pos} plus factor							{-> New expr.pos(factor.expr)}
		|	{neg} minus factor							{-> New expr.neg(factor.expr)}
		
		|	{num} number								{-> New expr.num(number)}
		|	{conchar} conchar							{-> New expr.conchar(conchar)}
		
		|	{lvalue} l_value							{-> l_value.expr}
		|	{func_call} func_call						{-> func_call.expr}
			
		|	{par} left_par expr right_par				{-> expr.expr}
		;
	
 
	l_value {-> expr} = 
		
			{id} id													{-> New expr.id(id)}
		|	{str} string	 										{-> New expr.str(string)}
		|	{expr} l_value left_bracket expr right_bracket			{-> New expr.array(l_value.expr, expr.expr)}
		;

	
	func_call {-> expr} = 
			{withpars} id left_par expr_list right_par 				{-> New expr.fun_call(id, [expr_list.expr])}
		|	{withoutpars} id left_par right_par			 			{-> New expr.fun_call(id, [])}
		;
	
	expr_list {-> expr*} = expr expr_list_tail* 					{-> [expr.expr, expr_list_tail.expr]} ;
	expr_list_tail {-> expr} = comma expr 							{-> expr.expr} ;



////////// CONDITIONS ///////////


	cond {-> cond} = 
	
			{or} cond or condterm								{-> New cond.or(condterm.cond, condrest.cond)}
		|	{exit} condterm										{-> condterm.cond}
		;
		
	condterm {-> cond} = 
	
			{and} condterm and condrest							{-> New cond.and(condterm.cond, condrest.cond)}
		|	{exit} condrest										{-> condrest.cond}
		;
		
		
	condrest {-> cond} =
	
			{not} not condrest									{-> New cond.not(condrest.cond)}
		|	{exit} condexit										{-> condexit.cond}
		;

	condexit {-> cond} = 
			
			{equal} [left]:expr equal [right]:expr				{-> New cond.equal(left.expr, right.expr)}
		|	{nequal} [left]:expr nequal [right]:expr			{-> New cond.nequal(left.expr, right.expr)}
		|	{less} [left]:expr less [right]:expr				{-> New cond.less(left.expr, right.expr)}
		|	{greater} [left]:expr greater [right]:expr			{-> New cond.greater(left.expr, right.expr)}
		|	{lesseq} [left]:expr lesseq [right]:expr			{-> New cond.lesseq(left.expr, right.expr)}
		|	{greatereq} [left]:expr greatereq [right]:expr		{-> New cond.greatereq(left.expr, right.expr)}
			
		|	{nest} left_par discond right_par					{-> discond.cond}
		;











Abstract Syntax Tree
	
	
	program = func_def;
	
	func_def = header local_def* block;
	
	header = 
			{withpars} id fpar_def* ret_type
		|	{withoutpars} id ret_type
		;
	
	
	fpar_def = 
			{withref} ref id* fpar_type 
		|	{withoutref} id* fpar_type
		;
		
	fpar_type = 
			{noarray} data_type
		|	{array_first} data_type helper
		|	{array_firstsec} data_type helper number*
		|	{array_sec} data_type number*
		;
	
	helper = left_bracket right_bracket ;
	
	ret_type = 
			{data} data_type
		|	{none} nothing
		;
		
	data_type =
			{int} integer
		|	{char} char
		;
	
	
	local_def =
			{funcdef} func_def
		|	{funcdecl} func_decl
		|	{vardef} var_def
		;
	
	
	type = 
			{array} data_type number*
		|	{noarray} data_type
		;
	
	
	
	var_def = id* type;
	
	func_decl = header;
	
	
	/////////////////// STMTS ///////////////////////
	
	block = stmt* ;
			
	
	stmt = 
			{none} semicolon
		|	{assignment} [left]:expr [right]:expr
		|	{block} block
		|	{func_call} expr
		|	{returnexpr} expr
		|	{return} semicolon
		
		|	{if} cond stmt
		|	{ifelse} cond [left]:stmt [right]:stmt
		
		|	{while} cond stmt
		|	
	
		;
		
	
	
	////////// EXRPESSIONS ///////////
	
	expr =
			{plus} [left]:expr [right]:expr
		|	{minus} [left]:expr [right]:expr
		|	{mult} [left]:expr [right]:expr
		|	{div} [left]:expr [right]:expr
		|	{mod} [left]:expr [right]:expr
		
	 	|	{pos} expr
		|	{neg} expr
		
		|	{conchar} conchar
	 	|	{num} number
		
		|	{id} id
		|	{str} string
		|	{array} [left]:expr [right]:expr
		
		|	{fun_call} id expr*
		
		;
	     
	 
	 ////////// CONDITIONS ///////////
	 
	cond =
	 		{not} cond
	 	|	{and} [left]:cond [right]:cond
	 	|	{or} [left]:cond [right]:cond
		
		|	{equal} [left]:expr [right]:expr	
	 	|	{nequal} [left]:expr [right]:expr
	 	|	{less} [left]:expr [right]:expr
	 	|	{greater} [left]:expr [right]:expr
		|	{lesseq} [left]:expr [right]:expr
		|	{greatereq} [left]:expr [right]:expr
		
		;


